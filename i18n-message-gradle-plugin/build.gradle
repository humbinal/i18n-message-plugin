plugins {
    id 'java'
    id "java-gradle-plugin"
    id 'maven-publish'
    id 'signing'
    id "com.gradle.plugin-publish" version "0.16.0"
}

dependencies {
    implementation gradleApi()
    implementation project(':i18n-message-plugin-core')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        i18nMessageGenerate {
            id = "io.github.humbinal.i18n.message.gradle-plugin"
            implementationClass = "io.github.humbinal.i18n.message.plugin.gradle.I18nMessagePlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/humbinal/i18n-message-plugin'
    vcsUrl = 'https://github.com/humbinal/i18n-message-plugin.git'
    description = 'gradle plugin for generate i18n message from source file written by javadoc.'
    tags = ['i18n', 'message', 'i18n-message', 'internationalization']

    plugins {
        i18nMessageGenerate {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle I18n Message Plugin'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options.author = true
    options.version = true
    options.header = project.name
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "i18n-message-gradle-plugin"
                description = "i18n-message-gradle-plugin"
                url = "https://github.com/humbinal/i18n-message-plugin"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = 'humbinal'
                        name = "humbinal"
                        email = "humbinal@126.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/humbinal/i18n-message-plugin.git"
                    developerConnection = "scm:git:https://github.com/humbinal/i18n-message-plugin.git"
                    url = "https://github.com/humbinal/i18n-message-plugin"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username NEXUS_USERNAME
                password NEXUS_PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}